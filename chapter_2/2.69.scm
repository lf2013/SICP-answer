; the thing worth doing typically take time and effort
; 2.69
(define (make-leaf symbol weight)
    (list 'leaf symbol weight))
(define (leaf? leaf) (equal? 'leaf (car leaf)))
(define (symbol-leaf leaf) (cadr leaf))
(define (weight-leaf leaf) (caddr leaf))

(define (make-code-tree l r)
    (list l r (append (symbols l) (symbols r)) (+ (weight l) (weight r))))
(define (left-branch tree) (car tree))
(define (right-branch tree) (cadr tree))
(define (symbols tree)
    (if (leaf? tree) (list (symbol-leaf tree))
        (caddr tree)))
(define (weight tree)
    (if (leaf? tree) (weight-leaf tree)
        (cadddr tree)))

(define (adjoin-set x set)
    (cond ((null? set) (list x))
          ((< (weight x) (weight (car set))) (cons x set))
          (else (cons (car set) (adjoin-set x (cdr set))))))

(define (make-leaf-set pairs)
    (if (null? pairs) '()
        (let ((pair (car pairs)))
            (adjoin-set (make-leaf (car pair) (cadr pair))
                (make-leaf-set (cdr pairs))))))

(define (successive-merge leaf-set)
    (cond ((null? leaf-set) '())
          ((null? (cdr leaf-set)) (car leaf-set))
          (else (successive-merge (adjoin-set (make-code-tree (car leaf-set) (cadr leaf-set)) (cddr leaf-set))))
    ))

(define (generate-huffman-tree pairs)
    (successive-merge (make-leaf-set pairs)))

(define (try)
    (define sample-pairs '((B 2) (A 4) (C 1) (D 1)))
    (display sample-pairs)
    (newline)
    (display (generate-huffman-tree sample-pairs))
)
